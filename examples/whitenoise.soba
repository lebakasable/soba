include std

const WIDTH 512
const HEIGHT 512
const PIXEL_SIZE 8

fn generate_pixels cols: u64 rows: u64 > u64 (
  (srand (time 0))

  let pixels (malloc (mul cols rows))
  for i (mul cols rows) do (
    let byte (and (rand) 255)
    (write (add pixels i) byte)
  )

  pixels
)

fn extern main > u64 (
  let PIXELS_COLS (div WIDTH PIXEL_SIZE)
  let PIXELS_ROWS (div HEIGHT PIXEL_SIZE)

  let pixels (generate_pixels PIXELS_COLS PIXELS_ROWS)
  defer (free pixels)

  let file (fopen "build/whitenoise.ppm" "wb")
  (assert_errno file "failed to open output file")
  defer (fclose file)

  (fprintf file "P6%c%d %d 255%c" '\n WIDTH HEIGHT '\n)

  let bytes (alloca 3)
  for y HEIGHT for x WIDTH do (
    let index (add (mul (div y PIXEL_SIZE) PIXELS_COLS)
                   (div x PIXEL_SIZE))
    let pixel (read (add pixels index))
    (write bytes pixel)
    (write (add bytes 1) pixel)
    (write (add bytes 2) pixel)
    (fwrite bytes 1 3 file)
  )

  0
)